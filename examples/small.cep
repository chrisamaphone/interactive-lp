%%%% Color changer %%%% 

red : pred.
blue : pred.
green : pred.

stage red_blue = {
  r1 : red -o blue.
}

stage blue_green = {
  r1 : blue -o green.
}

link1 : qui * stage red_blue -o stage blue_green.

context init = {red, red, red, red}.
context init' = {init, red}.

#trace _ red_blue init.

%%%% List sorter/minimum getter %%%% 

location : type.
start : location.
end : location.
l1 : location.
l2 : location.
l3 : location.
l4 : location.
l5 : location.

element : type.
r : element.
b : element.
g : element.

at location element location : pred.

succ element element : bwd.
succ/rb : succ r b.
succ/bg : succ b g.

lt element element : bwd.
lt/eq : lt X Y <- succ X Y.
lt/trans : lt X Z
          <- succ X Y
          <- lt Y Z.

leq element element : bwd.
leq/eq : leq X X.
leq/lt : leq X Y <- lt X Y.

stage sort = {
  swap : at L1 E1 L2 * at L2 E2 L * lt E2 E1
           -o at L1 E2 L2 * at L2 E1 L.
}

- : qui * stage sort -o stage getmin.
% possible alternative syntax:
% %qui stage sort -o stage getmin.

min element : pred.
stage getmin = {
  pluck : at start M _ -o min M.
  cleanup : min M * at _ _ _ -o min M.
}

context sort_init = 
{at start g l2, at l2 r l3, at l3 g l4,
  at l4 b l5, at l5 r end}.

#trace _ sort sort_init.

