%%% Ceptre simulation of sokoban game %%%

entity : type.
crate : entity.
player : entity.

cell: type.
cel1: cell.
cel2: cell.
cel3: cell.
cel4: cell.
cel5: cell.
cel6: cell.
cel7: cell.
cel8: cell.
cel9: cell.

% on C E  means   entity E is on cell C
on cell entity : pred.
% empty C means   cell C is empty (possible for the player/a crate to move
% onto)
empty cell : pred.

direction : type.
up : direction.
down : direction.
left : direction.
right : direction.

%  C1 C2 C3
%  C4 C5 C6
%  C7 C8 C9

% in_dir D C1 C2  means  cell C2 is in direction D from cell C1
in_dir direction cell cell : bwd.
in_dir right  cel1 cel2.
in_dir down   cel1 cel4.

in_dir left   cel2 cel1.
in_dir right  cel2 cel3.
in_dir down   cel2 cel5.

in_dir left   cel3 cel2.
in_dir down   cel3 cel6.

in_dir up     cel4 cel1.
in_dir right  cel4 cel5.
in_dir down   cel4 cel7. 

in_dir up     cel5 cel2.
in_dir left   cel5 cel4.
in_dir right  cel5 cel6.
in_dir down   cel5 cel8.

in_dir up     cel6 cel3.
in_dir left   cel6 cel5.
in_dir down   cel6 cel9.

in_dir up     cel7 cel4.
in_dir right  cel7 cel8.

in_dir up     cel8 cel5.
in_dir left   cel8 cel7.
in_dir right  cel8 cel9.

in_dir up     cel9 cel6.
in_dir left   cel9 cel8.

stage play = {

  %   [@][_]  -->  [_][@]
  move_empty : on C1 player * in_dir D C1 C2 * empty C2 -o empty C1 * on C2 player.

  %   [@][#][_] --> [_][@][#]
  move_push  : 
    on C1 player * in_dir D C1 C2 * on C2 crate * in_dir D C2 C3 * empty C3
    -o empty C1 * on C2 player * on C3 crate.

  disappear :
    on C1 crate * in_dir D C1 C2 * on C2 crate -o
      empty C1 * empty C2.

}
#interactive play.


context init = {empty cel2, empty cel6, empty cel7, empty cel8, empty cel9, 
on cel4 player, on cel1 crate, on cel3 crate, on cel5 crate}.

#trace _ play init.


